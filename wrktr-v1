#!/bin/zsh
# Set up a new workspace with links to shared resources

# https://www.tomups.com/posts/git-worktrees/
# mkdir project
# cd project
# git clone --bare ${repo_url} .bare
# echo "gitdir: ./.bare" > .git
# git config remote.origin.fetch "+refs/heads/*:refs/remotes/origin/*"
# git fetch
# git worktree add main
# git branch --list | grep -v '\*\|\+\|ahead' | xargs -n 1 git branch -d
# mkdir -p .SHARED/.claude
# touch .SHARED/CLAUDE.local.md

project_dir=$(pwd)

shared=${project_dir}/.SHARED
worktree=${project_dir}/$2

# files under .SHARED you want hardlinked
hardlink_assets=(
    "gitignore.Makefile"
    "CLAUDE.local.md"
    ".claude/settings.local.json"
)

# directories under .SHARED you want softlinked
softlink_assets=(
    ".claude/commands"
    ".claude/agents"
)

createHardlink() {
    filename="$1"
    ln -h ${shared}/${filename} ${worktree}/${filename}
}

createSoftlink() {
    filename="$1"
    ln -s ${shared}/${filename} ${worktree}/${filename}
}

_verifyExists() {
    filename="$1"
    test -e "${worktree}/${filename}"
}

verifyExists() {
    filename="$1"
    _verifyExists ${filename} && (echo "‚úÖ FOUND: ${worktree}/${filename}") || (echo "‚ùå MISSING: ${worktree}/${filename}")
}

verifySoftlink() {
    filename="$1"
    test -L "${worktree}/${filename}" && (echo "  ‚úì softlinked: ${worktree}/${filename}") || (echo "  ‚úò not softlinked: ${worktree}/${filename}")
}

verifyHardlink() {
    filename="$1"
    # link_count=$(stat -c %h "${worktree}/${filename}" 2>/dev/null)
    link_count=$(stat -f %l "${worktree}/${filename}" 2>/dev/null)
    [ "${link_count}" -gt 1 ] && (echo "  ‚úì hardlinked: ${worktree}/${filename}") || (echo "  ‚úò not hardlinked: ${worktree}/${filename}")
}

check() {
    echo "\nChecking for shared local resources in üìÅ ${worktree}...\n"
    _check
}

_check() {
    for asset in ${hardlink_assets}; do
        verifyExists ${asset} && verifyHardlink ${asset}
        echo
    done

    for asset in ${softlink_assets}; do
        verifyExists ${asset} && verifySoftlink ${asset}
        echo
    done
}

link() {
    mkdir -p ${worktree}/.claude

    for asset in ${hardlink_assets}; do
        _verifyExists ${asset} || createHardlink ${asset}
    done

    for asset in ${softlink_assets}; do
        _verifyExists ${asset} || createSoftlink ${asset}
    done

    echo "\nShared local resources linked in üìÅ ${worktree}\n"
    _check
}

cleanup() {
    for asset in ${hardlink_assets}; do
        _verifyExists ${asset} && unlink ${worktree}/${asset}
    done

    for asset in ${softlink_assets}; do
        _verifyExists ${asset} && unlink ${worktree}/${asset}
    done

    echo "\nShared local resources unlinked in üìÅ ${worktree}\n"
    _check
}

_exit() {
    unset project_dir
    unset shared
    unset worktree
    unset hardlink_assets
    unset softlink_assets
    exit "$1"
}

if test -d ${worktree}; then
    command -v "$1" &>/dev/null && "$1" || check
else
    echo "‚ùó worktree at ${worktree} does not exist" && _exit 1
fi

# vim: set et sts=4 ts=4 sw=4:
